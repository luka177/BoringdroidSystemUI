plugins {
	id "com.diffplug.spotless" version "5.11.0"
	id 'com.android.application'
}
apply plugin: 'kotlin-android'

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	kotlin {
		target '**/*.kt'
		ktlint()userData(['max_line_length': '100'])
	}
}

android {
	namespace 'com.boringdroid.systemui'
	compileSdkVersion 32

	defaultConfig {
		applicationId "com.boringdroid.systemui"
		minSdkVersion 32
		targetSdkVersion 32
		versionCode 100
		versionName "100"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	signingConfigs {
		debug {
			keyAlias 'androiddebugkey'
			keyPassword 'android'
			storeFile file('plugin.keystore')
			storePassword 'android'
		}
	}

	lintOptions {
		abortOnError false
	}

	testOptions {
		unitTests {
			all {
				testLogging {
					events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'

					outputs.upToDateWhen {
						false
					}

					showStandardStreams = false
					exceptionFormat = 'full'
				}
			}

			includeAndroidResources = true
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}
}

dependencies {
	compileOnly fileTree(include: ['*.jar'], dir: 'libs/compile')
	implementation fileTree(include: ['*.jar'], dir: 'libs/impl')
	// Current system use this version, we should keep it.
	implementation('androidx.recyclerview:recyclerview:1.2.0-alpha06') {
		exclude group: 'androidx.core'
		exclude group: 'androidx.annotation'
		exclude group: 'androidx.collection'
		exclude group: 'org.jetbrains', module: 'annotations'
	}
	configurations {
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
		implementation.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-io'
		implementation.exclude group: 'org.jetbrains', module: 'annotations'
	}





}
repositories {
	mavenCentral()
}
